linux: = import("os/linux")

print: proc(message: string) = linux.write(1, message.raw, message.length)
println: proc(message: string) =
{
  linux.write(1, message.raw, message.length)
  newline := "\n"
  linux.write(1, newline.raw, newline.length)
}

atoi: proc(value: string) -> i64 =
{
  result: i64

  factor: i64 = 1
  for index: i64 = value.length - 1, index >= 0, index -= 1
  {
    if index == 0 && value[0] == '-' break

    result += i64(value[index] - '0') * factor
    factor *= 10
  }

  if value[0] == '-'
  {
    result *= -1
  }

  return result
}

itoa: proc(value: i64, buffer: byte[]) -> string =
{
  length: u64 = 0

  negative: = value < 0
  if negative
  {
    value = -value
  }
  else if value == 0
  {
    buffer[length] = '0'

    length += 1
  }

  for value > 0
  {
    buffer[length] = byte(value % 10) + '0'

    value /= 10
    length += 1
  }

  if negative
  {
    buffer[length] = '-'

    length += 1
  }

  for index: u64, index < length / 2, index += 1
  {
    temp: = buffer[index]
    buffer[index] = buffer[length - 1 - index]
    buffer[length - 1 - index] = temp
  }

  return buffer[:length]
}

get_cstring_length: proc(value: ^byte) -> i64 =
{
  value_slice: byte[] = { raw = value, length = 999 /* TODO i64.max */ }

  length: i64
  for value_slice[length] != 0, length += 1 {}

  return length
}
